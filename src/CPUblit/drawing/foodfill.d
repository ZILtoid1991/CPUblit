module CPUblit.drawing.foodfill;

import CPUblit.colorspaces;

import CPUblit.drawing.common;

/** 
 * Implements a recursive scanline flood fill algorithm.
 * Params:
 *   x0 = X position of where the flood fill should happen.
 *   y0 = Y position of where the flood fill should happen.
 *   color = The color index to be written.
 *   dest = The destination buffer/image.
 *   destWidth = The width of the destination buffer/image.
 *   transparencyIndex = The index to be replace.
 */
public void floodFill(T)(int x0, int y0, T color, T[] dest, size_t destWidth, T transparencyIndex = T.init) 
		@nogc @safe nothrow pure {
	const size_t destHeight = dest.length / destWidth;
	void _fillLine(int x1, int y1) @nogc nothrow pure {
		if (dest[x1 + (y1 * destWidth)] != transparencyIndex) return;
		int x = x1;
		//Fill line to the right
		while (x < destWidth && dest[x + (y1 * destWidth)] == transparencyIndex) {
			dest[x + (y1 * destWidth)] = color;
			x++;
		}
		x = x1 - 1;
		//Fill line to the left
		while (x >= 0 && dest[x + (y1 * destWidth)] == transparencyIndex) {
			dest[x + (y1 * destWidth)] = color;
			x--;
		}
		x++;
		const int xBegin = x;
		//Test for scanlines above
		if (y1 > 0) {
			while (x < destWidth && dest[x + (y1 * destWidth)] == color) {
				if (dest[x + ((y1 - 1) * destWidth)] == transparencyIndex) {
					_fillLine(x, y1 - 1);
				}
				x++;
			}
		}
		x = xBegin;
		//Test for scanlines below
		if (y1 + 1 < destHeight) {
			while (x < destWidth && dest[x + (y1 * destWidth)] == color) {
				if (dest[x + ((y1 + 1) * destWidth)] == transparencyIndex) {
					_fillLine(x, y1 + 1);
				}
				x++;
			}
		}
	}
	if (transparencyIndex == color) return;
	_fillLine(x0, y0);
}
unittest {
	import std.stdio;
	ubyte[] area;
	area.length = 64;
	floodFill(0,0,0x1, area, 8);
	area = [
		0x0,0x0,0x0,0x0,0x0,0x0,0x0,0x0,
		0x0,0x0,0x3,0x3,0x3,0x3,0x0,0x0,
		0x0,0x3,0x0,0x0,0x0,0x0,0x3,0x0,
		0x0,0x3,0x0,0x0,0x0,0x0,0x3,0x0,
		0x0,0x3,0x0,0x0,0x0,0x0,0x3,0x0,
		0x0,0x3,0x0,0x0,0x0,0x0,0x3,0x0,
		0x0,0x0,0x3,0x3,0x3,0x3,0x0,0x0,
		0x0,0x0,0x0,0x0,0x0,0x0,0x0,0x0,
	];
	floodFill(3,3,0x4, area, 8);
	printMatrix(area, 8);
	area = [
		0x0,0x0,0x0,0x0,0x0,0x0,0x0,0x0,
		0x0,0x0,0x0,0x3,0x0,0x0,0x0,0x0,
		0x0,0x0,0x0,0x3,0x3,0x3,0x3,0x3,
		0x0,0x0,0x0,0x3,0x0,0x0,0x0,0x0,
		0x0,0x0,0x0,0x3,0x0,0x0,0x0,0x0,
		0x0,0x0,0x0,0x3,0x3,0x3,0x3,0x3,
		0x0,0x0,0x0,0x3,0x0,0x0,0x0,0x0,
		0x0,0x0,0x0,0x0,0x0,0x0,0x0,0x0,
	];
	floodFill(1,1,0x4, area, 8);
	printMatrix(area, 8);
}